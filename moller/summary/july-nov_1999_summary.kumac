MACRO july-nov_1999_summary fill=2
*
* ---      Summary for Jul-Nov 1999 polarization
*

 if [fill]>0 then
   exec read_tab1 fill=[fill] run1=7000 run2=8195
 endif

 titl 'Hall A beam beam polarization in Jul-Nov 1999'
 set csiz 0.3
 set dmod 0

 nm=$VLEN(krunm)
 x1=190
 x2=320
 y1=0.55
* y1=0.1
 y2=0.88
 null [x1] [x2] [y1] [y2]

 if $VDIM(dcurr,1)<40 then
   ve/cr dcurr(40) r
 endif
 
 if $VDIM(exx,1)<100 then
   ve/cr exx(100) r
 endif
 
 atit 'Days in 1999' 'Polarization'
 k1=0
 k2=0
 ymess=$SIGMA([y1]+([y2]-[y1])*0.97)
 dymes=$SIGMA(([y2]-[y1])*0.03)
 xmess=$SIGMA([x1]+([x2]-[x1])*0.7)
 dxmes=$SIGMA(([x2]-[x1])*0.1)

 set csiz 0.3
 do i=1,[nm]
   kk=0
   igset pmci 4
   set   hcol 4
   pol=polm([i],3)
   if [pol]>0.1 then
     sym=20
     cm=2
     k1=[k1]+1
     if [k1]=1 then
       kk=1
       txt='Moller, beam helicity +'
     endif
   else
     sym=22
     cm=4
     k2=[k2]+1
     if [k2]=1 then
       kk=1
       txt='Moller, beam helicity -'
     endif
   endif

   igset pmci [cm]
   set   hcol [cm]
   if [kk]>0 then
*      mess [xmess] [ymess]
      set csiz 0.25
      set ksiz 0.15
      gr/hpl/key [xmess] [ymess] [sym] [txt]
      set csiz 0.3
      set ksiz 0.28
      ymess=[ymess]-[dymes]       
*      mess [xmess] [ymess] [dymes]
   endif
   pp=polm([i],4)
   ep=$SIGMA([pp]*0.03)
   ve/in dcurr(1) [ep]

*   gr/hpl/err datm([i]) polm([i],4) exx epolm([i],4) 1 [sym] 0.15
   gr/hpl/err datm([i]) polm([i],4) exx dcurr 1 [sym] 0.15
 enddo 

 nmott=8
 ve/cr  dmotv([nmott]) r  193.6  201.9  222.8 224.0  232.0  232.8  257.9 300.7  
 ve/cr  pmotv([nmott]) r  0.738  0.670  0.800 0.760  0.780  0.745  0.660  0.660
 ve/cr epmotv([nmott]) r  0.009  0.030  0.009 0.020  0.030  0.030  0.030 0.050
 igset pmci 3
 set   hcol 3
 gr/hpl/err dmotv pmotv exx epmotv [nmott] 21 0.15

 set csiz 0.25
 set ksiz 0.15
 gr/hpl/key [xmess] [ymess] 21 'Mott '
 set csiz 0.3
 set ksiz 0.28
 ymess=[ymess]-[dymes]       

 set csiz 0.3
 set csiz 0.3

 igset pmci 1
 set   hcol 1

 ymess=$SIGMA([y1]+([y2]-[y1])*0.97)
 xmess=$SIGMA([x1]+([x2]-[x1])*0.5)

 en=0
 nn=$VDIM(runset,2)
 set dmod 3
 x0=[x1]
 y=$SIGMA([y1]+([y2]-[y1])/8.)
 kk=0
 igset plci 1
 igset txci 1
 do i=1,[nn]
   e=runset(8,[i])
   d=runset(7,[i])
   if [e]>0.1 .AND. [en]<>[e] .OR. [i]=[nn] then
     mess [i] [nn] [e] [en]
     if [i]=[nn] then
       d=[x2]
     endif      
     x=[d]-0.3
     if [kk]>0 then
       yy=$SIGMA([y1]+([y2]-[y1])*0.8)
       line [x] [y1] [x] [yy]
       xt=$SIGMA(([x0]+[x])/2.+0.2)       
       txt=[en]//' GeV'
       text [xt] [y] [txt] 0.2 90.
     endif
     en=[e]
     x0=[x]
     kk=[kk]+1
   endif
 enddo
 set csiz 0.25
 gr/hpl/key [xmess] [ymess] 3 'Energy change' ! L
 set csiz 0.3
 ymess=[ymess]-[dymes]       
 set dmod 0
 igset plci 1
 igset txci 1

 igset plci 2
 igset txci 2
 set dmod 2
 x=278.5
 y=$SIGMA([y1]+([y2]-[y1])*0.8)
 line [x] [y1] [x] [y]
 xt=[x]+2.
 y=$SIGMA([y1]+([y2]-[y1])*0.6)
 txt='Radiation damage'
 text [xt] [y] [txt] 0.2 90.
 set dmod 0
 igset plci 1
 igset txci 1


 d=1
 ve/cr dmonth(12) r 31. 28. 31. 30. 31. 30. 31. 31. 30. 31. 30. 31.
 do i=1,12
   x=[d]
   if [x]>[x1] .AND. [x]<[x2] then
      y=$SIGMA([y1]+([y2]-[y1])*0.03)
      gr/prim/arrow [x] [x] [y] [y1] 0.1
      txt=''
      if [i]=8 then
        txt=Aug
      elseif [i]=9 then
        txt=Sep
      elseif [i]=10 then
        txt=Oct
      elseif [i]=11 then
        txt=Nov
      endif
      x=[x]+0.2
      y=[y]+0.002
      txt1='1,'//[txt]
      text [x] [y] [txt1] 0.2 90.
   endif
   a=dmonth([i])
   d=[d]+[a]
 enddo

 ve/cr dspot(40) r 305.43 286.08 281.49 278.53 270.48 257.45 242.75 241.75 238.48 232.84 196.73 193.20
 ve/cr dcesi(40) r 296.55 291.62 284.70 283.42 280.53 277.40 269.51 268.71 265.62 263.65 237.51 
 ve/cr dngun(40) r 287.42 274.54 266.61 266.41 251.56

 do ity=1,3
   if [ity]=1 then
     col=6
     lmo=2
     np=$VLEN(dspot,1)
     ve/cop dspot dcurr
     txt='Spot move'
   elseif [ity]=2 then
     col=7
     lmo=4
     np=$VLEN(dcesi,1)
     ve/cop dcesi dcurr
     txt='Recesiation'
   elseif [ity]=3 then
     col=3
     lmo=13
     np=$VLEN(dngun,1)
     ve/cop dngun dcurr
     txt='New gun'
   endif
   igset plci [col]
*   igset txci [col]
   set dmod [lmo]
   do i=1,[np]
     x=dcurr([i])
     if [x]>[x1] .AND. [x]<[x2] then
        y=$SIGMA([y1]+([y2]-[y1])*0.8)
        yy=$SIGMA([y1]+([y2]-[y1])*0.2)
        gr/prim/dline [x] [x] [yy] [y] 
        x=[x]+0.2
        y=[y]+0.002
     endif
   enddo
   set csiz 0.25
   gr/hpl/key [xmess] [ymess] [lmo] [txt] ! L
   set csiz 0.3
   ymess=[ymess]-[dymes]
 enddo       
 
 igset plci 1
 igset txci 1
 set dmod 0

* call pol_sum_1.f

RETURN





MACRO analyze_set fill=1 ngr=1 delay=-1 nevadc=10 heladc=0 fillscal=0 ltmp=2 lout=3
*
* --- Analyze a set (or several sets)  of data
* --- input: fill=1 - read the files
*            ngr - number of sets to analyze
*            vectors iru(2,3,[ngr]) and ang(3,[ngr]) should be defined.
*            iru(1:2,[iang],[igr]) - runs range for target angle iang and set igr
*                  say, 10100,10106
*            ang([iang],[igr]) - target angle.
*               iang=1 - should be 90 deg
*            fillscal > 0 - fill the scaler ntuple
*
   if $VLEN(iru)<2 then
     mess '***' analyze_set error: vectors iru,ang not defined
     exitm
   endif

   if [fill]=0 .AND. [fillscal]=0 then
     goto eend
   endif
   nang=3

   ksc=0
   do igr=1,[ngr]
     do iang=1,[nang]
       r1=iru(1,[iang],[igr])
       r2=iru(2,[iang],[igr])
       angl=ang([iang],[igr])
       do ir=[r1],[r2]
         skip=0
         if $VLEN(irurej)>0 then
           do ii1=1,$VLEN(irurej)
             ir1=irurej([ii1])
             if [ir]=[ir1] then
               skip=1
             endif
           enddo
         endif
         mess Skip [ir] [skip]
         if [skip]=0 then
           exec run run=[ir]
           if [fill]>0 then

             exec asyms run=[ir] angl=[angl] delay=[delay] nevadc=[nevadc] heladc=[heladc]

           endif
           if [fillscal]>0 then
              l1=[ltmp]
              l2=0
              if [ksc]>0 then
                l1=-[ltmp]
              endif
              if [igr]=[ngr] .AND. [iang]=[nang] .AND. [ir]=[r2] then
                 l2=[lout]
              endif
              ksc=[ksc]+1
*              mess [ksc] ltmp=[l1] lout=[l2]
              exec asymwr run=[ir] angl=[angl] delay=[delay] nevadc=[nevadc] heladc=[heladc] ltmp=[l1] lout=[l2]
           endif
         endif
       enddo
     enddo  
   enddo

   eend:

   opt fit
   set fit 1111
   set csiz 0.5
*   zone 2 4

*   exec asym_av run1=10107 run2=10114
*   exec asym_av run1=10117 run2=10124

*   null 0 1 0 1
*   exec asym_av run1=10134 run2=10141
   
*   exec asym_av run1=10165 run2=10172
*   exec asym_av run1=10154 run2=10161

*   exec asym_av run1=10173 run2=10180
*   exec asym_av run1=10183 run2=10190


   titl 'Moller polarization'
   if $VDIM(polmea)<2 then
     ve/cr polmea(2,2) r
   endif
   do igr=1,[ngr]
     zone 2 2
     i90=1
     rp1=iru(1,1,[igr])
     rp2=iru(2,1,[igr])
     if [rp1]=0 .OR. [rp2]=0 then
        i90=0
     endif
     iav=1
     do iang=2,3
       r1=iru(1,[iang],[igr])
       r2=iru(2,[iang],[igr])
       if [r1]=0 .OR. [r2]=0 then
          null 0 1 0 1
          iav=0
       else
         exec asym_av run1=[r1] run2=[r2] atit=0 titl=0 kill=1
         pol=para(1)
         mess Pol=[pol]
         i1=[iang]-1
         ve/in polmea([i1],1) [pol]
         angl=ang([iang],[igr])
         txt='Runs '//[r1]//' '//[r2]//' angle '//[angl]
         atit [txt] ''
         if [i90]>0 then
           exec targ_angl run1=[r1] run2=[r2] runn1=[rp1] runn2=[rp2] i=1
           a1=targcal(1)
           exec targ_angl run1=[r1] run2=[r2] runn1=[rp1] runn2=[rp2] i=2
           a2=targcal(1)
           angl=$RSIGMA([angl])
           angl1=$RSIGMA(([a1]+[a2])/2.)
           if [angl]>100. then
             angl1=$RSIGMA(180.-[angl1])
           endif
           cmnd='chngpol.f('//[pol]//','//[angl]//','//[angl1]//')'
           pol1=$CALL([cmnd])
           ve/in polmea([i1],2) [pol1]
           null 0 10 0 10
           txt='Angle initial ='//[angl]//' Polar='//[pol]
           text 1 8 [txt] 0.25 0
           txt='Angles ='//[a1]//', '//[a2]//' aver='//[angl1]
           text 1 7 [txt] 0.25 0
           txt=' Polar='//[pol1]
           text 1 6 [txt] 0.25 0
         endif
       endif
     enddo
     if [iav]>0 then
       p1=polmea(1,1)
       p2=polmea(2,1)
       polav1=$SIGMA(([p1]+[p2])/2.)
       p1=polmea(1,2)
       p2=polmea(2,2)
       polav2=$SIGMA(([p1]+[p2])/2.)
       txt='Average polarizations '//[polav1]//' , '//[polav2]
       text 1 3 [txt] 0.25 0
       polavav=$SIGMA(([polav1]+[polav2])/2.)
       txt='Average of averages '//[polavav]
       text 1 2 [txt] 0.25 0

     endif
  
   enddo

RETURN









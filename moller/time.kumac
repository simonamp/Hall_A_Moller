MACRO time lun=0 run=0 id0=-1 fill=1 log=0 n1=155 n2=190 clear=1 siz=0.4
*
* Fills and plots time historams
*
* --- id0 histogram offset
* --- lun NTUPLE unit
* --- fill = 0 - only fit/plot
* --- clear >0 - erase all histograms at the beginning for a new run

 run0=0
 if $VDIM(irun,1)>0 then
   run0=irun(1)
 endif
 exec getrun run=[run] lun=[lun]
 lun=ilun(1)
 run=irun(1)

 if [lun]=0 then
   mess Abort ...
   exitm
 endif
 if [id0]=-1 then
   id0=[run]*1000 
   mess Set id0 to [id0]
 endif

*
*--- Clear the old histograms?
*
 if [clear]>0 .AND. [run] .NE. [run0] then
   cd //pawc
   h/del 0
 endif   


 tit='Moeller run='//[run]//' timing' 
 titl [tit]

 ve/cr idd0(1) i [id0]

 ve/cr itdc(13) i 1 2 3 4 5 6 7 8 18 19 20

 opt fit
 next
 set csiz [siz]

 if [fill]>0 then
   do i=1,32
     id=[id0]+200+[i]
     tit='TDC '//[i]//' 1 [m]s range ' 
     1dh [id] [tit] 1100 0. 1100.
     id=[id0]+300+[i]
     tit='TDC '//[i]//' the latest hit ' 
     1dh [id] [tit] 300 0. 300.
     id=[id0]+400+[i]
     tit='TDC '//[i]//' hits per channel ' 
     1dh [id] [tit] 17 0. 17.
   enddo

   id=[id0]+340
   1dh [id] 'total hits in each channel' 32 1. 33.
   id=[id0]+350
   2dh [id] 'time min 1 vs 2 ' 100 100. 200. 100 100. 200. 16000.
   id=[id0]+360
   1dh [id] 'Coincidence statistics ' 20 1 21
   id=[id0]+370
   1dh [id] 'Number of hits ' 4 1 5
   id=[id0]+380
   1dh [id] 'Coincidence statistics, ADC>200 ' 20 1 21
   id=[id0]+501
   2dh [id] 'time last vs prev ' 100 100. 200. 100 100. 200. 16000.
   id=[id0]+613
   2dh [id] 'time last vs prev ' 100 50. 150. 100 0. 100. 16000.
   id=[id0]+513
   2dh [id] 'time last vs prev ' 100 0. 100. 100 0. 200. 16000.
   id=[id0]+713
   1dh [id] 'time last vs prev ' 40 50. 90. 
   id=[id0]+714
   1dh [id] 'time last vs prev ' 40 90. 130. 
   id=[id0]+813
   1dh [id] 'time last vs prev ' 200 0. 200. 

   nt/loop //lun[lun]/1 nadc>11.and.rdntup.f(1.)>0 
 endif

 ve/cre zord(15) i
 i=0
 do k=1,3
   do j=1,5
     i=[i]+1
     m=[k]+3*([j]-1) 
     ve/in zord([i]) [m]
   enddo
 enddo

 next
 set stat 1111
 set csiz [siz]
 set ywin 1.2
 set yhti 0.7
 zone 3 5
 do i=1,5
   m=zord([i])
   if [i]>1 then
     zone 3 5 [m] s
   endif
   id=[id0]+300+[i]
   nx=$HINFO([id],'XBINS')
   if $VDIM(tmp)<[nx] then
     ve/cr tmp([nx]) r
   endif
   h/get_vec/con [id] tmp
   con=0.
   do j=[n1],[n2]
     a=tmp([j])
     con=[con]+[a]
   enddo

   h/pl [id]([n1]:[n2])
   opt ntic
   null 0 1 0 1 sab
   opt tic
   txt=[con]//' hits'
   text 0.1 0.8 [txt] 0.2
 enddo

 ve/cre zord(21) i
 i=0
 do k=1,3
   do j=1,7
     i=[i]+1
     m=[k]+3*([j]-1) 
     ve/in zord([i]) [m]
   enddo
 enddo
* exitm
 opt liny
 if [log]>0 then
   opt logy
 endif
 opt nsta
 do j=1,13
   i=itdc([j])
   if [i]>0 then
     m=zord([j])
     m=[m]+1
     zone 3 7 [m] s
     id=[id0]+200+[i]
     h/pl [id](1:300)
   endif
 enddo
 m=zord(14)
 m=[m]+1
 zone 3 7 [m] s
 id=[id0]+360
 opt liny
 h/pl [id]
 

 set csiz 0
 set stat 1111111
 opt liny
 opt sta
RETURN

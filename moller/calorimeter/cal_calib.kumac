MACRO cal_calib lun=lun21 run=0 t1=190 t2=205 id=5000 x1=0.1 x2=2.1 cut=0 niter=5 fill=1 update=1 i1=1 i2=16 corr=1 n=16 adc1=13

   if [run]>0 then
     exec getrun run=[run] 
     lun=ilun(1)
     run=irun(1)

     if [lun]=0 then
       mess Abort ...
       exitm
     endif
     lun=lun//[lun]
   endif

   titl 'Run '//[run]//' calorimeter calibration'

   if [fill]=0 then
     goto fitl
   endif

   nto=$SIGMA([adc1]+[n]-1)
   if $VDIM(calibtmp)<[nto] then
     ve/cr calibtmp([nto]) r
     ve/cop calib(1:[nto]) calibtmp(1:[nto])
     ve/wri calibtmp calibtmp.dat
   endif

   if $VDIM(restmp)<[n] then
     ve/cr restmp([n]) r
   endif

   if $VDIM(ichlim,2)<4 then
     mess Error: ichlim, ilglim are not defined (see files cal_ini?.kumac)
     goto eend 
   endif

   ve/cr para(10) r
   ve/cr epara(10) r

   id1=[id]+400
   txt='Energy in '//[n]//' modules vs HOD coordinate'
   h/cr/prof [id1] [txt] 16 0 8. 0.1 2.1 ' '
   do i=1,[n]
     id1=[id]+[i]
     txt='En. '//[n]//'mod-s, cen='//[i]
     1dh [id1] [txt] 200 [x1] [x2]
     id1=[id]+[i]+100
     txt='Energy perif/all, center='//[i]
     1dh [id1] [txt] 200 0. 1.
     id1=[id]+[i]+200
     txt='Particle energy, center='//[i]
     1dh [id1] [txt] 200 0. 4.
     id1=[id]+[i]+300
     txt='Energy in '//[n]//'moduls, center='//[i]//' vs HOD channel'
     2dh [id1] [txt] 200 [x1] [x2] 16 1 17
     id1=[id]+[i]+400
     id2=[id]+[i]
     txt='Energy in '//[n]//'modules, center='//[i]//' vs HOD coordinate'
     h/cop [id2] [id1]
     id1=[id]+[i]+500
     txt='ADC , center='//[i]
     1dh [id1] [txt] 120 0. 1000.
   enddo

   cut $20 nadc>0.and.cal_hmul.f([i1],[i2],[t1],[t2])=1
   if [cut]=0 then
     cut $21 $20
   elseif [cut]=-1 then
     cut $21 nadc>0
   else
     cut $21 $20.and.$[cut]
   endif

   fitl:

   do iter=1,[niter]
    if [fill]>0 then 
      id1=[id]+400
      h/op/res [id1]
      do i=1,[n]
        id1=[id]+[i]
        h/op/res [id1]
        id1=[id]+[i]+100
        h/op/res [id1]
        id1=[id]+[i]+200
        h/op/res [id1]
        id1=[id]+[i]+300
        h/op/res [id1]
        id1=[id]+[i]+400
        h/op/res [id1]
        id1=[id]+[i]+500
        h/op/res [id1]
      enddo

*      nt/loop //[lun]/1 nadc>0.and.$21.and.cal_calib.f([n],[adc1],[i1],[i2],[t1],[t2],[corr],[id])>-1
      nt/loop //[lun]/1 nadc>0.and.cal_calib.f([n],[adc1],[i1],[i2],[t1],[t2],[corr],[id])>-1
    endif

*     zone 3 3
    do ich=1,[n]
       id1=[id]+[ich]
       a=$HINFO([id1],'MEAN')
       c=$HINFO([id1],'MAX')
       b=[a]*0.1
       ve/in para(1:3) [c] [a] [b]
       h/fit [id1] g 0 3 para ! ! ! epara
       jch=$SIGMA([adc1]-1+[ich])
       a=calibtmp([jch])
       b=para(2)
       w=para(3)
       res=0.
       c=[a]
       id2=[id]+[ich]+200
       emean=$HINFO([id2],'MEAN')
       if [b]>0 then
         c=[a]*1./[b]
         res=$SIGMA(ABS([w])/[b]*sqrt([emean]))
       endif
       if [update]>0 then
         ve/in calibtmp([jch]) [c]
         ve/in restmp([ich]) [res]
       endif

       tit=$HTITLE([id1])
       ind=$INDEX([tit],', mean')
       if [ind]>0 then
         ind1=[ind]-1
         tit1=$SUBSTRING([tit],1,[ind1])
         tit=[tit1]
       endif
       emf=$FORMAT([emean],F4.2)
       tit=[tit]//', mean E='//[emf]
       h/cop [id1] 99 
       h/cop 99 [id1] [tit]
       h/pl [id1]
*       mess id1=[id1] [ich] [tit]
       opt ntic
       null 0 1 0 1 sab
       res1=$FORMAT([res],F5.3)
       tit='resol='//[res1]//' GeV^0.5!'
       text 0.05 0.9 [tit] 0.25
       opt tic
    enddo
    ve/pri calibtmp
    ve/pri restmp
   enddo  

   ve/wri calibtmp calibtmp.dat

   eend:
RETURN










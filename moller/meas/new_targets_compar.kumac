MACRO new_targets_compar plot=1
*
* Plots the results from 2005 aug-october (new target)
*
 nm=6
 nt=5
*       8A
 ve/cr  pol([nt],[nm]) r
 ve/cr epol([nt],[nm]) r

 ve/in  pol(1:5,1) 88.9  -1   84.7  85.6  87.0
 ve/in epol(1:5,1) 0.17   0   0.20  0.25  0.30

 ve/in  pol(1:5,2) -1   83.6  82.9  -1    84.4
 ve/in epol(1:5,2)  0   0.40  0.20   0    0.20

 ve/in  pol(1:5,3) -1   -1    82.8  83.0  84.2
 ve/in epol(1:5,3)  0    0    0.20  0.21  0.20

* 10 sept 05
 ve/in  pol(1:5,4) 87.5  84.2 83.7  85.3  85.7
 ve/in epol(1:5,4) 0.12  0.20 0.16  0.20  0.20

* 26 sept 05
 ve/in  pol(1:5,5) -1    84.3 82.9  85.8  85.6
 ve/in epol(1:5,5) 0.    0.20 0.20  0.20  0.20

* 24 oct  05
 ve/in  pol(1:5,6) 89.4  87.5 86.0  88.7  89.0
 ve/in epol(1:5,6) 0.14  0.20 0.20  0.20  0.20

 ve/cr  pdif([nt],[nm]) r
 ve/cr epdif([nt],[nm]) r
 ve/cr targn(10) r 1 2 3 4 5 6 7 8 9 10
 ve/cr err(100) r 
 ve/cr pav([nm]) r 
 ve/cr epav([nm]) r 

 if [plot]=1 then
   null 0 7 -4 4
   atit 'Measurement' 'Beam Polarization, target 5 subtracted, percent'
 endif
 ve/cr mcol([nt]) i  1  2  3  4  6  
 ve/cr mtyp([nt]) i 20 21 22 23 24 
 
 do im=1,[nm]
   p0=pol(5,[im])
   ep0=epol(5,[im])
   av=0.
   eav=0.
   mm=0
   do it=1,[nt]
     p=pol([it],[im])
     ep=epol([it],[im])
     dp=$SIGMA(([p]-[p0])/[p]*100)
     edp=$SIGMA(SQRT([ep]**2+[ep0]**2)/[p]*100)
     if [p]<10 then
       dp=99
       edp=0
     endif
     ve/in  pdif([it],[im]) [dp] 
     ve/in epdif([it],[im]) [edp]
     if [dp]<90 .AND. [it]<>5 then
       col=mcol([it])
       typ=mtyp([it]) 
       igset pmci [col]
       if [plot]=1 then
         gr/hpl/err targn([im]) pdif([it],[im]) err(1) epdif([it],[im]) 1 [typ] 0.3
       endif
       igset pmci 1
     endif
     if [dp]<90 .AND. [it]>1 then
*       mess [im] [it] [p]
       av=$SIGMA([av]+[p]/[ep]**2)
       eav=$SIGMA([eav]+1./[ep]**2)
       mm=[mm]+1
     endif
   enddo
   if [mm]>0 then
     av=$SIGMA([av]/[eav])
     eav=$SIGMA(SQRT(1./[eav]))
     ve/in pav([im]) [av]
     ve/in epav([im]) [eav]
   endif
 enddo

 xm=5
 ym=3

 do it=1,[nt]
   if [it]<> 5 then
     col=mcol([it])
     typ=mtyp([it]) 
     igset pmci [col]
     text='target '//[it]
     if [plot]=1 then
       gr/hpl/key [xm] [ym] [typ] [text] 
     endif
     ym=$SIGMA([ym]-0.4)
     igset pmci 1
   endif
 enddo

 if [plot]=1 then
   set dmod 4
   line 0 0 7 0
   set dmod 1
 endif

 if [plot]=2 then
   null 0 7 82. 90.
   atit 'Measurement' 'Beam Polarization, percent'
   igset pmci 2
   gr/hpl/err targn(1:[nm]) pav(1:[nm]) err(1:[nm]) epav(1:[nm]) [nm] 20 0.35
   igset pmci 1
   xm=0.2
   sz=0.35
   text [xm] 89.  'Meas 1 - 08 aug 2005' [sz]
   text [xm] 88.5 'Meas 2 - 26 aug 2005' [sz]
   text [xm] 88.  'Meas 3 - 10 sep 2005' [sz]
   text [xm] 87.5 'Meas 4 - 26 sep 2005' [sz]
   text [xm] 87.  'Meas 5 - 17 oct 2005' [sz] 
   text [xm] 86.5 'Meas 6 - 24 oct 2005' [sz]
 endif

 exitm
 ve/cr  pd([nm]) r
 ve/cr epd([nm]) r

 do it=1,[nt]
   mm=0
   do im=1,[nm]
     p=pdif([it],[im])
     if [p]<90 then
       mm=[mm]+1
       ve/in  pd([mm]) [p]
       ve/in epd([mm]) epdif([it],[im]) 
     endif
   enddo
   ve/fit targn(1:[mm]) pd(1:[mm]) epd(1:[mm]) p0
 enddo

 RETURN
